plugins {
    id 'com.android.application'
    id 'kotlin-android'
//    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
}

def keystorePropertiesFile = rootProject.file("app.properties")
def attrs = new Properties()
attrs.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias attrs['keyAlias']
            keyPassword attrs['password']
            storeFile file(attrs['storeFile'] == null ? "build.gradle" : attrs['storeFile'])
            storePassword attrs['password']
        }
    }
    compileSdk 35
    defaultConfig {
        applicationId "com.romellfudi.ussd"
        minSdk 23
        targetSdk 35
        versionCode 13
        versionName "1.13.a"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["clearPackageData"] = "true"
        signingConfig signingConfigs.config
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
            applicationIdSuffix ".demo"
//            versionNameSuffix "-demo"
        }
        publish {
            dimension "version"
            versionNameSuffix "-publish"
        }
    }
    android.variantFilter { variant ->
        if ((variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'demo')
                || (variant.buildType.name == 'debug'
                && variant.getFlavors().get(0).name == 'publish'))
            variant.setIgnore(true)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
//    VERSION_1_8
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }
    lint {
        abortOnError false
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "${project.rootProject.name}-${variant.name}.apk"
        }
    }
    namespace = "com.romellfudi.ussd"
}


dependencies {
    implementation project(':ussd-library-kotlin')
//    implementation 'com.romellfudi.permission:kotlin-fudi-permission:2.0.a'
    implementation 'com.github.romellfudi.FudiPermission:kotlin-fudi-permission:2.0.b'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

//    implementation 'com.google.android.play:core:1.10.3'
// This dependency is downloaded from the Googleâ€™s Maven repository.
    // Make sure you also include that repository in your project's build.gradle file.
    implementation("com.google.android.play:app-update:2.1.0")

    // For Kotlin users, also import the Kotlin extensions library for Play In-App Update:
    implementation("com.google.android.play:app-update-ktx:2.1.0")

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

//    implementation "org.koin:koin-android:$koin"
//    implementation "org.koin:koin-androidx-viewmodel:$koin"
    implementation(libs.koin.core)
    implementation(libs.koin.android)


    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.airbnb.android:lottie:6.6.2'
    implementation 'com.google.android.material:material:1.12.0'
}
dependencies {
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.4.0'
}
